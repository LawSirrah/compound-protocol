#!/usr/bin/env yarn repl -s

PrintTransactionLogs

-- Token holder addresses for mocking
Alias CompHolder "0x7587caefc8096f5f40acb83a09df031a018c66ec"
Alias MakerHolder "0x05e793ce0c6027323ac150f6d45c2344d28b6019"
Alias CUSDCHolder "0x5e34bc93a7506ecc8562ade4d5c8b090247a6349"

Web3Fork "https://mainnet-eth.compound.finance/@12884400" (CompHolder MakerHolder CUSDCHolder)
UseConfigs mainnet

-- Delegate and propose update
From CompHolder (Comp Delegate CompHolder)
From CompHolder (GovernorBravo GovernorBravoDelegator Propose "Add MKR Market" [(Address Comptroller) (Address Comptroller) (Address cMKR)] [0 0 0] ["_supportMarket(address)" "_setCollateralFactor(address,uint256)" "_setReserveFactor(uint256)"] [[(address cMKR)] [(address cMKR) 0] [250000000000000000]])

-- Fast forward, vote, queue, execute
MineBlock
AdvanceBlocks 14000
From CompHolder (GovernorBravo GovernorBravoDelegator Proposal LastProposal Vote For)
AdvanceBlocks 20000
GovernorBravo GovernorBravoDelegator Proposal LastProposal Queue
IncreaseTime 604910
GovernorBravo GovernorBravoDelegator Proposal LastProposal Execute

-- Assert cMKR market supported
Assert True (Comptroller CheckListed cMKR)

-- Ensure accrue interest works
CToken cMKR AccrueInterest

-- Mint test
From MakerHolder (Erc20 MKR Approve (Address cMKR) 10e18)
From MakerHolder (CToken cMKR Mint 10e18)
Assert Equal (Erc20 MKR TokenBalance cMKR) (10e18)


-- Borrow test
From CUSDCHolder (CToken cMKR Borrow 1e18)
Assert Equal (Erc20 MKR TokenBalance CUSDCHolder) (1e18)
Assert Equal (Erc20 MKR TokenBalance cMKR) (9e18)

-- Repay borrow test
From CUSDCHolder (Erc20 MKR Approve (Address cMKR) 1e18)
From CUSDCHolder (CToken cMKR RepayBorrow 1e18)
Assert Equal (Erc20 MKR TokenBalance CUSDCHolder) (0)
Assert Equal (Erc20 MKR TokenBalance cMKR) (10e18)

-- Redeem test (note: 50 cMKR == 1 MKR initially)
From MakerHolder (CToken cMKR Redeem 50e8)

Print "cMKR integration ok"
